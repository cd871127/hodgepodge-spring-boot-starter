import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        maven {
            url aliyunMevenRepo
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {

    group 'io.github.cd871127.hodgepodge'
    version '0.0.1-SHAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'


    repositories {
        maven {
            url = aliyunMevenRepo
        }
    }


    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    dependencies {
        annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    }

}


subprojects {
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            maven(MavenPublication) {
                //指定group/artifact/version信息，可以不填。默认使用项目group/name/version作为groupId/artifactId/version
                groupId project.group
                artifactId project.name
                version project.version
                //如果是war包填写components.web，如果是jar包填写components.java
                from components.java

                //配置上传源码
                artifact sourceJar {
                    classifier "sources"
                }

            }
        }
//        repositories {
//            maven {
//                //指定要上传的maven私服仓库
//                url = "http://jenkins.maxrocky.com:8080/maven/content/repositories/thirdparty/"
//                //认证用户和密码
//                credentials {
//                    username 'admin'
//                    password 'maxrocky5721'
//                }
//            }
//        }
    }
}








